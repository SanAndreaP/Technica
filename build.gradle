// For those who want the bleeding edge
buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url  = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = 'CodeChicken'
            url  = 'http://chickenbones.net/maven/'
        }
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
        classpath 'codechicken:CodeChickenCore:1.8-1.0.5.36:dev'
        classpath 'codechicken:NotEnoughItems:1.8-1.0.5.104:dev'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = "0.3.5.1"
group= "com.mlb.technica" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "technica"

minecraft {
    version = "1.8-11.14.3.1503"
    runDir = "eclipse"
    mappings = "snapshot_20141130"
}

task downloadCCLDev(type:LibDownloader) {
    sourceUrl = 'http://files.minecraftforge.net/maven/codechicken/CodeChickenLib/1.8-1.1.2.139/CodeChickenLib-1.8-1.1.2.139-dev.jar'
    target    = file('libs/CodeChickenLib-dev.jar')
}

task downloadCCLSrc(type:LibDownloader) {
    sourceUrl = 'http://files.minecraftforge.net/maven/codechicken/CodeChickenLib/1.8-1.1.2.139/CodeChickenLib-1.8-1.1.2.139-src.jar'
    target    = file('libs/CodeChickenLib-src.jar')
}



repositories {
    maven {
        name = 'CodeChicken'
        url  = 'http://chickenbones.net/maven/'
    }
    maven {
        name = 'forge'
        url  = 'http://files.minecraftforge.net/maven'
    }

}

dependencies {
    compile 'codechicken:CodeChickenCore:1.8-1.0.5.36:dev'
    compile 'codechicken:CodeChickenCore:1.8-1.0.5.36:src'
    compile 'codechicken:NotEnoughItems:1.8-1.0.5.104:dev'
    compile 'codechicken:NotEnoughItems:1.8-1.0.5.104:src'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

idea { module { inheritOutputDirs = true } }

class LibDownloader extends DefaultTask {
    @Input
    String sourceUrl

    @OutputFile
    File target

    @TaskAction
    void download() {
        ant.get(src: sourceUrl, dest: target)
    }
}